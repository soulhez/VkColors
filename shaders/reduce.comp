#version 450
#extension GL_ARB_separate_shader_objects : enable

layout(local_size_x_id = 0) in;
layout (constant_id = 1) const int pyramidLevels = 24;

layout(push_constant) uniform Info {
    uint count;
    uint targetLevel;
} info;

struct Element {
    uint index;
    uint score;
};

layout(set = 0, binding = 0) buffer Pyramid {
    Element[] elements;
} pyramid[pyramidLevels];

void main() {
    if (gl_GlobalInvocationID.x * 2 >= info.count) {
        Element empty;
        empty.index = 0;
        empty.score = uint(-1);
        pyramid[info.targetLevel].elements[gl_GlobalInvocationID.x] = empty;
        return;
    }

    uint left = gl_GlobalInvocationID.x * 2;
    uint right = left + 1;

    Element leftE = pyramid[info.targetLevel + 1].elements[left];
    Element rightE = pyramid[info.targetLevel + 1].elements[right];

    if (right < info.count) {
        if (leftE.score < rightE.score) {
            pyramid[info.targetLevel].elements[gl_GlobalInvocationID.x] = leftE;
        } else {
            pyramid[info.targetLevel].elements[gl_GlobalInvocationID.x] = rightE;
        }
    } else {
        pyramid[info.targetLevel].elements[gl_GlobalInvocationID.x] = leftE;
    }
}