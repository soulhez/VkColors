#version 450
#extension GL_ARB_separate_shader_objects : enable

#define UMAX uint(-1)

layout(local_size_x_id = 0) in;
layout(constant_id = 1) const uint maxWorkGroups = 1;

layout(push_constant) uniform Info {
    uint count;
} info;

layout(set = 0, binding = 0, rgba8i) uniform iimage2D image;

layout(set = 0, binding = 1) buffer Positions {
    ivec2[] data;
} positions;

layout(set = 0, binding = 2) buffer Colors {
    ivec4[] data;
} colors;

struct Score {
    uint score;
    uint index;
};

layout(set = 0, binding = 3) buffer Output {
    Score[] scores;
} outputData;

int length2(ivec3 v) {
    return v.x * v.x + v.y * v.y + v.z * v.z;
}

void main() {
    uint offset = (gl_WorkGroupID.y * maxWorkGroups) + gl_WorkGroupID.x;
    outputData.scores[offset].score = UMAX;
    outputData.scores[offset].index = UMAX;

    barrier();

    if (gl_GlobalInvocationID.x >= info.count) {
        return;
    }

    ivec2 pos = positions.data[gl_GlobalInvocationID.x];
    ivec2[8] neighbors = ivec2[](
        pos + ivec2(-1, -1),
        pos + ivec2(-1,  0),
        pos + ivec2(-1,  1),
        pos + ivec2( 0, -1),
        pos + ivec2( 0,  1),
        pos + ivec2( 1, -1),
        pos + ivec2( 1,  0),
        pos + ivec2( 1,  1)
    );

    uint sum = 0;
    uint count = 0;
    ivec4 testColor = colors.data[gl_WorkGroupID.y];

    for (int i = 0; i < 8; i++) {
        ivec2 n = neighbors[i];
        ivec4 color = imageLoad(image, n);
        if (color.a != 0) {
            sum += length2(testColor.rgb - color.rgb);
            count++;
        }
    }

    uint bestScore = uint(sum / float(count));
    
    atomicMin(outputData.scores[offset].score, bestScore);

    barrier();

    if (outputData.scores[offset].score == bestScore) {
        atomicMin(outputData.scores[offset].index, gl_GlobalInvocationID.x);
    }
}